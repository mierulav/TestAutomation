'***********************************************************
' Module		: Connect Homepage 
' Created by	: amirul.saddam@dksh.com
' Last modified	: 27-4-2021
' Modified by	: amirul.saddam@dksh.com

'***********************************************************
OPTION EXPLICIT

Public Homepage : Set Homepage = Browser("DKSH Connect").Page("Homepage")
Public DKSHLogo : Set DKSHLogo = HomePage.Image("Header-DKSHLogo")
Public AllProducts : Set AllProducts = HomePage.Link("Menu-All Products")
Public Reward : Set Reward = HomePage.Link("Menu-Reward")
Public Pay : Set Pay = HomePage.Link("Menu-Pay")
Public Promotion : Set Promotion = HomePage.Link("Menu-Promotion")
Public BonusProgram : Set BonusProgram = HomePage.Link("Menu-BonusProgram")
Public Statement : Set Statement = HomePage.Link("Menu-Statement")
Public TrackOrder : Set TrackOrder = HomePage.Link("Menu-TrackOrder")
Public ResourceCenter : Set ResourceCenter = Homepage.Link("Menu-ResourceCenter")
Public SearchBar : Set SearchBar = HomePage.WebEdit("Header-Searchbar")
Public SearchButton : Set SearchButton = HomePage.WebElement("Header-SearchButton")
Public FooterContactDetails : Set FooterContactDetails = HomePage.WebElement("Footer-ContactDetails")
Public FooterPrivacyPolicy : Set FooterPrivacyPolicy = HomePage.WebElement("Footer-PrivacyPolicy")
Public FooterTermsCondition : Set FooterTermsCondition = HomePage.WebElement("Footer-TermsComdition")
Public FooterTermsUse : Set FooterTermsUse = HomePage.WebElement("Footer-TermsUse")
Public CarouselHeadline : Set CarouselHeadline = HomePage.WebElement("Content-CarouselHeadline")
Public CarouselItem : Set CarouselItem = HomePage.WebElement("Content-CarouselItem")
Public ShipToDD : Set ShipToDD = HomePage.WebElement("Header-ddShioTo")
Public ShipToNameDD : Set ShipToNameDD = HomePage.WebElement("Header-ddShipToName")
Public Minicart : Set Minicart = HomePage.WebElement("Header-MiniCart")
Public ShipToSelectBtn : Set ShipToSelectBtn = HomePage.WebElement("selectbuttonshiptoname")
Public ShipToList : Set ShipToList = HomePage.WebList("shiptoname")
Public IndustryDD : Set IndustryDD = HomePage.WebList("Header-ddIndustry")
Public LanguageDD : Set LanguageDD = HomePage.WebList("Header-ddLang")
Public UserMenuAccountManager : Set UserMenuAccountManager = Homepage.Link("User-AccountManager")
Public UserMenuContactUs : Set UserMenuContactUs = Homepage.Link("User-ContactUs")
Public UserMenuDeliveryAddress : Set UserMenuDeliveryAddress = Homepage.Link("User-DeliveryAddress")
Public UserMenuFavoritesList : Set UserMenuFavoritesList = Homepage.Link("User-FavoritesList")
Public UserMenuPassword : Set UserMenuPassword = Homepage.Link("User-Password")
Public UserMenuPersonalDetails : Set UserMenuPersonalDetails = Homepage.Link("User-PersonalDetails")
Public UserMenuSignOut : Set UserMenuSignOut = Homepage.Link("User-SignOut")
Public UserMenuSavedOrder : Set UserMenuSavedOrder = Homepage.Link("User-SavedOrder")

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
' Simple operations
'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

'==================================================================
'Function to Search
'==================================================================

 Public Sub SearchProduct(strVal)
 	
 	SearchBar.RefreshObject
 	SearchBar.WaitProperty "Visible", True
 	SearchBar.Set strVal
 	SearchButton.Click
 	PLPProductName.RefreshObject
 	PLPProductName.WaitProperty "Visible", True
 	 	
 End Sub
 
'==================================================================
'Function to Navigate to Home page
'==================================================================

  Public Sub OpenHomePage()
 
 	ObjectSync Homepage
 	DKSHLogo.RefreshObject
 	DKSHLogo.Click
 	
 End Sub
 
'==================================================================
'Function to Navigate to All Product page
'==================================================================

  Public Sub OpenAllProductPage()
 
 	AllProducts.RefreshObject
 	AllProducts.WaitProperty "Visible", True, 3000
 	AllProducts.Click
 	Dim intMaxAttempts : intMaxAttempts = 0
 	Do Until (Browser("DKSH Connect").Page("Product").WebElement("PageLoader").GetROProperty("Visible") = False Or intMaxAttempts = 25)
 		Wait(1)
 		intMaxAttempts = intMaxAttempts + 1
  	loop
 	
 	If intMaxAttempts = 25 Then
 		AssertExitRun "Navigate to All Product Page", "Taking too long to load more than 15 seconds"
 		OpenHomePage
 	End If
 End Sub
 
'==================================================================
'Function to Navigate to Track page
'==================================================================
 
 Public Sub OpenTrackOrderPage()
 	
	''Homepage.Sync
	Select Case ProjectName
		Case "TWHEC"
			Browser("DKSH Connect").Page("Homepage").Link("Menu-TrackOrder-TW").Click
		Case Else
			TrackOrder.RefreshObject
			TrackOrder.WaitProperty "Visible", True
			TrackOrder.Click	
	End Select
	
 End Sub
 
 '==================================================================
'Function to Navigate to Favorites List
'==================================================================
 
  Public Sub OpenFavoritesList()
 
 	ShipToNameDD.RefreshObject
 	ShipToNameDD.WaitProperty "Visible", True
 	ShipToNameDD.Click
 	UserMenufavoritesList.RefreshObject
 	UserMenufavoritesList.Click
 	
  End Sub
 
 
 
'==================================================================
'Function to Select ShipTo
'==================================================================

  Public Sub SelectShipToDefault()
 	
	ShipToSelectBtn.RefreshObject
	If ShipToSelectBtn.Exist(5) Then
		ShipToSelectBtn.Click
	End If
	
 End Sub
 
 '==================================================================
'Function to Select ShipTo
'==================================================================

  Public Function SelectShipToAddress(strVal)
 	
'	SelectShipToAddress = Nothing
	Dim i
	If ShipToList.Exist <> True Then
		SelectShipToAddress = True
		Exit Function
	End If
	Dim arrVal : arrVal = Split(ShipToList.GetROProperty("all items"), ";")
	For i = 0 To Ubound(arrVal)
		If Instr(arrVal(i), strVal) > 0 Then
			ShipToList.Select arrVal(i)
			SelectShipToAddress = True
			Exit For
		End If		
		If i = Ubound(arrVal) and Instr(arrVal(i), strVal) < 0 Then
			SelectShipToAddress = False
			Exit For
		End If
	Next
	
	ShipToSelectBtn.WaitProperty "Visible", True
	ShipToSelectBtn.Click
	
	arrVal = empty
	strVal = empty
	
 End Function
 
 Public Sub LogoutAndCloseBrowser()
 
 	ShipToNameDD.RefreshObject
 	ShipToNameDD.WaitProperty "Visible", True
 	ShipToNameDD.Click
 	UserMenuSignOut.RefreshObject
 	UserMenuSignOut.Click
 	Browser("creationtime:=0").Close
 	
  End Sub
  
   Public Sub Logout()
 
 	ShipToNameDD.RefreshObject
 	ShipToNameDD.WaitProperty "Visible", True
 	ShipToNameDD.Click
 	UserMenuSignOut.RefreshObject
 	UserMenuSignOut.Click
 	
  End Sub
 '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
 ' Header Checking
 ' 1. Localization
 ' 2. Search
 ' 3. User menu
 ' 4. ShipTo selection
 ' 5. Mini cart
 '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
 
 '=================================================================
' Function to check Carousel objects
'=================================================================

 Public Function CheckCarouselObjects()
	
	Dim DicObj, i, x, k
	
	Set DicObj = CreateObject("Scripting.Dictionary")
	
	Homepage.Sync
	Homepage.RefreshObject
	
	DicObj.Add "CarouselHeadline", CarouselHeadline.Exist(1)
	DicObj.Add "CarouselItem", CarouselItem.Exist(1)
	
	i = DicObj.Items
	k = DicObj.Keys
		
	For x = 0 To DicObj.Count-1
		'print k(x) & ", " & i(x)
		If i(x) <> "True" Then
			CheckCarouselObjects = False
			Exit For
		End If
		CheckCarouselObjects = True
	Next
	
	Set DicObj = nothing
	
End Function
 
'=================================================================
' Function to check header objects
'=================================================================

 Public Function CheckHeaderObjects()
	
	Dim DicObj, i, x, k
	
	Set DicObj = CreateObject("Scripting.Dictionary")
	
	Homepage.Sync
	Homepage.RefreshObject
	
	DicObj.Add "Minicart", Minicart.Exist(1)
	DicObj.Add "SearchButton", SearchButton.Exist(1)
	DicObj.Add "SearchBar", SearchBar.Exist(1)
	DicObj.Add "DKSHLogo", DKSHLogo.Exist(1)
	DicObj.Add "ShipToDD", ShipToDD.Exist(1)
	DicObj.Add "ShipToNameDD", ShipToNameDD.Exist(1)
	DicObj.Add "LanguageDD", LanguageDD.Exist(1)
		
	i = DicObj.Items
	k = DicObj.Keys
	
	For x = 0 To DicObj.Count-1
		'print k(x) & ", " & i(x)
		If i(x) <> "True" Then
			CheckHeaderObjects = False
			Exit For
		End If
		CheckHeaderObjects = True
	Next
	
	Set DicObj = nothing
	
End Function

'=================================================================
' Function to check Navigation menu objects
'=================================================================

 Public Function CheckNavigationMenuObjects()
	
	Dim DicObj, i, x, k
	
	Set DicObj = CreateObject("Scripting.Dictionary")
	
	Homepage.Sync
	Homepage.RefreshObject
	
	DicObj.Add "AllProducts", AllProducts.Exist(1)
	DicObj.Add "TrackOrder", TrackOrder.Exist(1)
	DicObj.Add "Resource Center", ResourceCenter.Exist(1)
	
	Select Case ProjectName
	
		Case "AUTEC", "SGCG", "THHEC"
			DicObj.Add "Pay", Pay.Exist(1)
		
		Case "MYHEC"
			DicObj.Add "Reward", Reward.Exist(1)
			DicObj.Add "Statement", Statement.Exist(1)
		
		Case "VNHEC"
			DicObj.Add "Reward", Reward.Exist(1)
			DicObj.Add "Promotion", Promotion.Exist(1)
			DicObj.Add "Bonus Program", BonusProgram.Exist(1)
	End Select
	
	i = DicObj.Items
	k = DicObj.Keys
		
	For x = 0 To DicObj.Count-1
		'print k(x) & ", " & i(x)
		If i(x) <> "True" Then
			CheckNavigationMenuObjects = False
			Exit For
		End If
		CheckNavigationMenuObjects = True
	Next
	
	Set DicObj = nothing
	
End Function

'=================================================================
' Function to check Footer objects
'=================================================================

 Public Function CheckFooterObjects()
	
	Dim DicObj, i, x, k
	
	Set DicObj = CreateObject("Scripting.Dictionary")
	
	Homepage.Sync
	Homepage.RefreshObject
	
	DicObj.Add "FooterContactDetails", FooterContactDetails.Exist(1)
	DicObj.Add "FooterPrivacyPolicy", FooterPrivacyPolicy.Exist(1)
	DicObj.Add "FooterTermsUse", FooterTermsUse.Exist(1)
	
	Select Case ProjectName
		Case "AUTEC", "MYHEC"
			DicObj.Add "FooterTermsCondition", FooterTermsCondition.Exist(1)
			
		Case "TWHEC"
			DicObj.Add "FooterTermsCondition", FooterTermsCondition.Exist(1)
			DicObj.Remove  "FooterTermsUse"
		Case Else
		
	End Select
	
	i = DicObj.Items
	k = DicObj.Keys
	
	For x = 0 To DicObj.Count-1
		'print k(x) & ", " & i(x)
		If i(x) <> "True" Then
			CheckFooterObjects = False
			Exit For
		End If
		CheckFooterObjects = True
	Next
	
	Set DicObj = nothing
	
End Function

'=================================================================
' Function to check User menu list
'=================================================================

Public Function CheckUserMenuList(strRole)

	ShipToNameDD.Click
	
	Dim DicObj, i, x, k
	
	Set DicObj = CreateObject("Scripting.Dictionary")
	
	Homepage.Sync
	Homepage.RefreshObject
	
	DicObj.Add "Saved Order", UserMenuSavedOrder.Exist(1)
	DicObj.Add "Favorites List", UserMenuFavoritesList.Exist(1)
	DicObj.Add "Personal Details", UserMenuPersonalDetails.Exist(1)
	DicObj.Add "Password", UserMenuPassword.Exist(1)
	DicObj.Add "Delivery Address", UserMenuDeliveryAddress.Exist(1)
	DicObj.Add "Contact Us", UserMenuContactUs.Exist(1)
	DicObj.Add "Sign Out", UserMenuSignOut.Exist(1)
	DicObj.Add "Account Manager", UserMenuAccountManager.Exist(1)
	
	Select Case LCase(strRole)
		
		Case "normal"
			DicObj.Remove "Account Manager"
			DicObj.Add "Account Manager", Not UserMenuAccountManager.Exist(1)
			
	End Select
	
	i = DicObj.Items
	k = DicObj.Keys
	
	For x = 0 To DicObj.Count-1
		'print k(x) & ", " & i(x)
		If i(x) <> "True" Then
			CheckUserMenuList = False
			Exit For
		End If
		CheckUserMenuList = True
	Next
	
	Set DicObj = nothing
	
End Function

'=================================================================
' Function to check language available 
'=================================================================

Public Function CheckLocalization()
	
	Homepage.Sync
	LanguageDD.RefreshObject
	LanguageDD.WaitProperty "Visible", True, 5
	Dim strLanguage : strLanguage = LanguageDD.GetROProperty("all items")
	CheckLocalization = False
	
	Select Case ProjectName
		Case "AUTEC", "MYHEC", "SGCG"
			If strLanguage = "ENG" Then
				CheckLocalization = True
			End If
			
		Case "KHHEC"
			If InStr(strLanguage, "ENG") > 0 And InStr(strLanguage, "KHM") > 0 Then
				CheckLocalization = True
			End If
			
		Case "MMHEC"
			If InStr(strLanguage, "ENG") > 0 And InStr(strLanguage, "BUR") > 0 Then
				CheckLocalization = True
			End If
		
		Case "VNHEC"
			If InStr(strLanguage, "ENG") > 0 And InStr(strLanguage, "VIE") > 0 Then
				CheckLocalization = True
			End If
			
		Case "THHEC"
			If InStr(strLanguage, "ENG") > 0 And InStr(strLanguage, "ไทย") > 0 Then
				CheckLocalization = True
			End If
			
		Case "TWHEC"
			If InStr(strLanguage, "ENG") > 0 And InStr(strLanguage, "繁體中") > 0 Then
				CheckLocalization = True
			End If
	End Select
	
End Function



